import org.gradle.util.GradleVersion

initscript {
    def pluginVersion = "3.6"

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath("com.gradle:gradle-enterprise-gradle-plugin:${pluginVersion}")
    }
}

def isTopLevelBuild = gradle.getParent() == null

if (isTopLevelBuild) {
    def version = GradleVersion.current().baseVersion
    def atLeastGradle5 = version >= GradleVersion.version("5.0")
    def atLeastGradle6 = version >= GradleVersion.version("6.0")

    if (atLeastGradle6) {
        settingsEvaluated { settings ->
            settings.extensions["gradleEnterprise"].with {
               buildScan {
                   buildScanPublished { scan ->
                       logScan(file("scans.csv"), scan)
                   }
               }
            }
        }
    } else if (atLeastGradle5) {
        projectsEvaluated { gradle ->
            gradle.rootProject.extensions["buildScan"].with {
                buildScanPublished { scan ->
                    logScan(file("scans.csv"), scan)
                }
            }
        }
    }
}

def logScan(scanFile, scan) {
    def port = (scan.buildScanUri.port != -1) ? ":" + scan.buildScanUri.port : ""
    def baseUrl = "${scan.buildScanUri.scheme}://${scan.buildScanUri.host}${port}"
    scanFile.append("${baseUrl},${scan.buildScanId},${scan.buildScanUri}\n")
}
