def isTopLevelBuild = gradle.getParent() == null
if (isTopLevelBuild) {
    if (!gradle.startParameter.buildCacheEnabled) {
        throw new GradleException("Build caching is not enabled. Please enable build caching and run this experiment again.")
    }

    settingsEvaluated { settings ->
        settings.buildCache {
            local {
                enabled = true
                push = true
                directory = new File(experimentDir, "build-cache")
            }
            if (remote != null) {
                remote.enabled = false
            }
        }
    }
} else {
    throw new IllegalStateException("Build validation only supported on top-level Gradle build.")
}

File getExperimentDir() {
    def projectProperties = gradle.startParameter.projectProperties
    new File(projectProperties.get("com.gradle.enterprise.init.script.experimentDir"))
}
