import org.gradle.util.GradleVersion

def isTopLevelBuild = gradle.getParent() == null

if (isTopLevelBuild) {
    def version = GradleVersion.current().baseVersion
    def atLeastGradle5 = version >= GradleVersion.version("5.0")
    def atLeastGradle6 = version >= GradleVersion.version("6.0")
    def projectProperties = gradle.startParameter.projectProperties
    def experimentDir = new File(projectProperties.get("com.gradle.enterprise.init.script.experimentDir"))

    if (atLeastGradle6) {
        settingsEvaluated { settings ->
            registerCallbacks(settings.extensions["gradleEnterprise"], experimentDir)
        }
    } else if (atLeastGradle5) {
        projectsEvaluated { gradle ->
            registerCallbacks(gradle.rootProject.extensions["gradleEnterprise"], experimentDir)
        }
    } else {
        throw new GradleEnterprise("The Gradle version (${GradleVersion.current()} is too old to run the experiment. Please upgrade to at least Gradle 5.")
    }
}

def registerCallbacks(gradleEnterprise, rootDir) {
    gradleEnterprise.with {
        buildScan {
            buildScanPublished { scan -> logScan(rootDir, scan) }
            onError { error -> reportFailedToPublishBuildScan(rootDir, error) }
        }
    }
}

def logScan(rootDir, scan) {
    def port = (scan.buildScanUri.port != -1) ? ":" + scan.buildScanUri.port : ""
    def baseUrl = "${scan.buildScanUri.scheme}://${scan.buildScanUri.host}${port}"

    def scanFile = new File(rootDir, "scans.csv")
    scanFile.append("${baseUrl},${scan.buildScanUri},${scan.buildScanId}\n")
}

def reportFailedToPublishBuildScan(rootDir, error) {
    def errorFile = new File(rootDir, "build-scan-publish-error.txt")
    errorFile.text = error
}
